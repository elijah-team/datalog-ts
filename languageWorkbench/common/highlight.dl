hl.Segment{type: T, id: I, span: S, highlight: H} :-
  hl.segmentNumber{type: T, id: I, span: S, highlight: H} |
  hl.segmentString{type: T, id: I, span: S, highlight: H} |
  hl.segmentBool{type: T, id: I, span: S, highlight: H} |
  hl.keyword{type: T, id: I, span: S, highlight: H} |
  hl.segmentIdent{type: T, id: I, span: S, highlight: H} |
  hl.segmentSpecialVar{type: T, id: I, span: S, highlight: H} |
  hl.segmentIdentDefnHL{type: T, id: I, span: S, highlight: H} |
  hl.segmentIdentUsageHL{type: T, id: I, span: S, highlight: H} |
  hl.segmentComment{type: T, id: I, span: S, highlight: H}.

hl.NonHighlightSegment{type: T, span: S} :-
  hl.segmentNumber{type: T, span: S} |
  hl.segmentString{type: T, span: S} |
  hl.segmentBool{type: T, span: S} |
  hl.keyword{type: T, span: S} |
  hl.segmentIdent{type: T, span: S} |
  hl.segmentSpecialVar{type: T, span: S} |
  hl.segmentComment{type: T, span: S}.

# primitives
hl.segmentNumber{type: "number", id: I, span: S, highlight: false} :-
  hl.mapping{rule: R, type: "number"} &
  astInternal.node{rule: R, id: I, span: S}.
hl.segmentString{type: "string", id: I, span: S, highlight: false} :-
  hl.mapping{rule: R, type: "string"} &
  astInternal.node{rule: R, id: I, span: S}.
hl.keyword{type: "keyword", id: I, span: S, highlight: false} :-
  hl.mapping{rule: R, type: "keyword"} &
  astInternal.node{rule: R, id: I, span: S}.
hl.segmentBool{type: "boolean", id: I, span: S, highlight: false} :-
  hl.mapping{rule: R, type: "boolean"} &
  astInternal.node{rule: R, id: I, span: S}.
hl.segmentComment{type: "comment", id: I, span: S, highlight: false} :-
  hl.mapping{rule: R, type: "comment"} &
  astInternal.node{rule: R, id: I, span: S}.

# identifiers
hl.segmentSpecialVar{type: "typeParameter", span: S, highlight: false} :-
  hl.mapping{rule: R, type: "typeParameter"} &
  astInternal.node{rule: R, span: S}.
hl.segmentIdent{type: "variable", id: I, span: S, highlight: false} :-
  hl.mapping{rule: R, type: "ident"} &
  astInternal.node{rule: R, parentID: I, span: S}.

# identifiers with cursor
hl.segmentIdentDefnHL{type: "defn", span: S, highlight: true} :-
  ide.CurrentDefnOrDefnOfCurrentVar{span: S}.
hl.segmentIdentUsageHL{type: "usage", span: S, highlight: true} :-
  ide.CurrentVarOrUsageOfCurrentDefn{span: S}.
