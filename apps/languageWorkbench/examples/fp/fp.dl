scope.Scope{id: I, label: I} :-
  ast.expr{id: I}.

scope.Defn{scopeID: I, span: S, name: N, kind: "var"} :-
  ast.expr{id: I} &
  ast.letExpr{id: LetExprID, parentID: I} &
  ast.ident{parentID: LetExprID, text: N, span: S}.

scope.Var{scopeID: I, name: N, span: S, kind: "var"} :-
  ast.expr{id: I} &
  ast.varExpr{parentID: I, text: N, span: S} |
  ast.expr{id: I} &
  ast.funcCall{id: FuncCallID, parentID: I} &
  ast.varExpr{parentID: FuncCallID, text: N, span: S}.

scope.Parent{childID: ChildID, parentID: ParentID} :-
  ast.expr{id: ParentID} &
  astInternal.node{parentID: ParentID, id: IntermediateID} &
  ast.expr{id: ChildID, parentID: IntermediateID}.

scope.Placeholder{scopeID: I, span: S, kind: "var"} :-
  ast.expr{id: I} &
  ast.placeholder{parentID: I, span: S}.

hl.mapping{rule: "intLit", type: "int"}.
hl.mapping{rule: "stringLit", type: "string"}.
hl.mapping{rule: "bool", type: "bool"}.
hl.mapping{rule: "ident", type: "ident"}.
hl.mapping{rule: "letKW", type: "keyword"}.
hl.mapping{rule: "inKW", type: "keyword"}.

lang.Builtin{name: "plus2", type: tapp{from: "int", to: "int"}}.

ast.RootExpr{id: 1}.

tc.Type{id: I, type: T} :-
  tc.typeS{id: I, type: T} |
  tc.typeI{id: I, type: T} |
  tc.typeLambda{id: I, type: T} |
  tc.typeFC{id: I, type: T} |
  tc.typeLet{id: I, type: T} |
  tc.typeVar{id: I, type: T} |
  tc.typePlaceholder{id: I, type: T}.

tc.typeS{id: I, type: "string"} :-
  ast.expr{id: I} &
  ast.stringLit{parentID: I}.
tc.typeI{id: I, type: "int"} :-
  ast.expr{id: I} &
  ast.intLit{parentID: I}.
tc.typeFC{id: I, type: T} :-
  ast.expr{id: I} &
  ast.funcCall{parentID: I, id: FC} &
  ast.funcCallArg{callID: FC, argID: AID} &
  ast.funcCallFunc{callID: FC, funcID: FID} &
  tc.Type{id: FID, type: tapp{from: F, to: T}} &
  tc.Type{id: AID, type: F}.
tc.typeLet{id: I, type: T} :-
  ast.expr{id: I} &
  ast.letExpr{id: L, parentID: I} &
  ast.letBodyExpr{letID: L, bodyID: BID} &
  tc.Type{id: BID, type: T}.
# TOOO: convert to scope.Item
tc.typeVar{id: I, type: T} :-
  ast.expr{id: I} &
  ast.varExpr{id: V, parentID: I, text: N} &
  tc.ScopeItem{id: I, name: N, type: T}.
# TODO: get this to work for multiple parameters...
tc.typeLambda{id: I, type: tapp{from: F, to: R}} :-
  ast.expr{id: I} &
  ast.lambda{id: L, parentID: I, retType: R, body: B} &
  ast.lambdaParam{lambdaID: L, ty: F} &
  tc.Type{id: B, type: R}.
tc.typePlaceholder{id: I, type: "unknown"} :-
  ast.expr{id: I} &
  ast.Placeholder{id: P, parentID: I}.

tc.ScopeItem{id: I, name: N, type: T, location: L} :-
  tc.scopeBuiltin{id: I, name: N, type: T, location: L} |
  tc.scopeLet{id: I, name: N, type: T, location: L} |
  tc.scopeLambda{id: I, name: N, type: T, location: L} |
  tc.parentScope{id: I, parentID: P} & tc.ScopeItem{id: P, name: N, type: T, location: L}.

tc.scopeBuiltin{id: I, name: N, type: T, location: "builtin"} :-
  ast.RootExpr{id: I} &
  lang.Builtin{name: N, type: T}.
tc.scopeLet{id: I, name: N, type: T, location: S} :-
  ast.letBodyExpr{letID: L, bodyID: I} &
  ast.letBindingExpr{letID: L, bindingID: B} &
  ast.letVar{letID: L, name: N, span: S} &
  tc.Type{id: B, type: T}.
tc.scopeLambda{id: I, name: N, type: T, location: L} :-
  ast.lambda{body: I, id: LID} &
  ast.lambdaParam{lambdaID: LID, name: N, ty: T, location: L}.

tc.parentScope{id: I, parentID: P} :-
  ast.letExpr{id: L, parentID: P} &
  ast.letBindingExpr{letID: L, bindingID: I} |
  ast.letExpr{id: L, parentID: P} &
  ast.letBodyExpr{letID: L, bodyID: I} |
  ast.funcCall{id: C, parentID: P} &
  ast.funcCallArg{callID: C, argID: I} |
  ast.funcCall{id: C, parentID: P} &
  ast.funcCallFunc{callID: C, funcID: I}.
  # ast.lambda{body: I, id: P}.

# parents
ast.letBindingExpr{letID: L, bindingID: D} :-
  ast.letExpr{letID: L} &
  ast.inKW{id: IN, parentID: L} &
  astInternal.next{prev: WS, next: IN} &
  astInternal.next{prev: D, next: WS}.
ast.letBodyExpr{letID: L, bodyID: I} :-
  ast.letExpr{letID: L} &
  ast.inKW{id: IN, parentID: L} &
  astInternal.next{prev: IN, next: WS} &
  astInternal.next{prev: WS, next: I}.
ast.letVar{letID: L, name: N, span: S} :-
  ast.letExpr{id: L} &
  ast.ident{parentID: L, text: N, span: S}.
ast.funcCallArg{callID: C, argID: I} :-
  ast.funcCall{id: C} &
  ast.expr{parentID: C, id: I}.
ast.funcCallFunc{callID: C, funcID: F} :-
  ast.funcCall{id: C} &
  ast.parenExpr{id: P, parentID: C} &
  ast.expr{id: F, parentID: P}.
# ast.lambdaBody{lambdaID: L, bodyID: B} :-
#   XXX.
